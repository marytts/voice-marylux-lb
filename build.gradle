buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.17'
    }
}

plugins {
    id 'de.dfki.mary.voicebuilding-legacy' version '5.2.0'
    id 'de.undercouch.download' version '3.1.1'
}

group 'de.dfki.mary'
version '5.2'

voice {
    name = 'marylux'
    language = 'lb'
    gender = 'female'
    type = 'unit selection'
    description = 'A female Luxembourgish unit selection voice'
    license {
        name = 'Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International'
        shortName = 'CC-BY-NC-SA 4.0'
        url = 'https://creativecommons.org/licenses/by-nc-sa/4.0/'
    }
    samplingRate = 16000
}

ext {
    flacFile = file("$buildDir/marylux-data.flac")
    yamlFile = file("$buildDir/marylux-data.yaml")
}

import de.undercouch.gradle.tasks.download.Download
import org.yaml.snakeyaml.Yaml

task downloadAudio(type: Download) {
    src "https://github.com/marytts/marylux-data/releases/download/v0.1.1/$flacFile.name"
    dest flacFile
    overwrite false
    acceptAnyCertificate true
}

task downloadData(type: Download) {
    src "https://github.com/marytts/marylux-data/releases/download/v0.1.1/$yamlFile.name"
    dest yamlFile
    overwrite false
    acceptAnyCertificate true
}

task extractLab {
    dependsOn downloadData
    def destDir = file("$buildDir/lab")
    inputs.file yamlFile
    outputs.dir destDir
    doFirst {
        destDir.mkdirs()
    }
    doLast {
        new Yaml().load(yamlFile.newReader()).each { utterance ->
            if (utterance.segments) {
                file("$destDir/${utterance.prompt}.lab").withWriter { writer ->
                    writer.println '#'
                    def end = 0.0
                    utterance.segments.each { segment ->
                        end += segment.dur
                        writer.println "${end.round(6)} 125 $segment.lab"
                    }
                }
            }
        }
    }
}

task extractText {
    dependsOn downloadData
    def destDir = file("$buildDir/text")
    inputs.file yamlFile
    outputs.dir destDir
    doFirst {
        destDir.mkdirs()
    }
    doLast {
        new Yaml().load(yamlFile.newReader()).each { utterance ->
            if (utterance.segments) {
                file("$destDir/${utterance.prompt}.txt").withWriter { writer ->
                    writer.println utterance.text
                }
            }
        }
    }
}

task extractWav {
    dependsOn downloadData, downloadAudio
    ext.destDir = file("$buildDir/wav")
    inputs.files yamlFile, flacFile
    outputs.dir destDir
    doFirst {
        destDir.mkdirs()
    }
    doLast {
        new Yaml().load(yamlFile.newReader()).each { utterance ->
            if (utterance.segments) {
                exec {
                    commandLine 'sox', flacFile, "$destDir/${utterance.prompt}.wav",
                            'trim', utterance.start, "=$utterance.end",
                            'rate', voice.samplingRate
                }
            }
        }
    }
}

legacyInit {
    dependsOn extractLab, extractText, extractWav
    def basenameListFile = file("$buildDir/basenames.lst")
    outputs.files basenameListFile
    doLast {
        basenameListFile.withWriter { basenameList ->
            fileTree(wav.destDir).collect {
                it.name - '.wav'
            }.minus('lb-wiki-0018').each {
                basenameList.writeLine it
            }
        }
    }
}
