plugins {
    id 'de.dfki.mary.voicebuilding-legacy' version '5.2.0'
    id 'de.undercouch.download' version '3.1.1'
}

group 'de.dfki.mary'
version '5.2-SNAPSHOT'

voice {
    name = 'marylux'
    language = 'lb'
    gender = 'female'
    type = 'unit selection'
    description = 'A female Luxembourgish unit selection voice'
    license {
        name = 'Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International'
        shortName = 'CC-BY-NC-SA 4.0'
        url = 'https://creativecommons.org/licenses/by-nc-sa/4.0/'
    }
    samplingRate = 16000
}

ext {
    flacFile = file("$buildDir/marylux-data.flac")
    yamlFile = file("$buildDir/marylux-data.yaml")
}

import de.undercouch.gradle.tasks.download.Download

task downloadAudio(type: Download) {
    src "https://github.com/marytts/marylux-data/releases/download/v0.1.1/$flacFile.name"
    dest flacFile
    overwrite false
    acceptAnyCertificate true
}

task downloadData(type: Download) {
    src "https://github.com/marytts/marylux-data/releases/download/v0.1.1/$yamlFile.name"
    dest yamlFile
    overwrite false
    acceptAnyCertificate true
}

evaluationDependsOn('data')

project('data').ext.sampleRate = voice.samplingRate

wav {
    dependsOn project('data').wav
    srcDir project('data').wav.destDir
}

task text(type: Copy) {
    from project('data').text
    into "$buildDir/text"
}

task lab(type: Copy) {
    from project('data').lab
    into "$buildDir/lab"
}

task generateBasenamesList {
    def basenamesListFile = file("$buildDir/basenames.lst")
    inputs.files lab
    outputs.files basenamesListFile
    doLast {
        basenamesListFile.withWriter { dest ->
            fileTree(lab.destinationDir).include('lb*.lab').findAll {
                !it.name.startsWith('lb-wiki-0018')
            }.each {
                dest.println it.name - '.lab'
            }
        }
    }
}

legacyInit {
    dependsOn wav, text, lab, generateBasenamesList
}
